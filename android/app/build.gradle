def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new FileNotFoundException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 30
    ndkVersion '22.0.7026061'
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.equalitie.ouisync_app"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        ndk{
            //TODO arm64 platform creates issues in MacOS (Catalina - 10.15.7)
            //To get it to compile, filters for the supported abis (x86,x86_64,armeabi-v7a)
            //are passed to the ndk via a property in the local.properties file, so it is
            //specific to each machine. If such property doesn't exist then this is not performed.
            //To add another architecture, just add the property and all the architectures supported
            if (localProperties.containsKey('ndk.abiFilters')) {
                abiFilters = []
                abiFilters.addAll(
                        localProperties
                                .getProperty('ndk.abiFilters')
                                .split(',')
                                .collect{it as String}
                )
            }
        }

    }

    buildTypes {
        release {
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
            // Note: Android SDK comes with its own cmake but its version is too low
            // to build ouisync. Thus install a recent cmake on your system and use that.
            // To do so modify cmake.dir in your android/local.properties file as
            // explained here
            //
            // https://stackoverflow.com/questions/40966472/can-android-studio-use-the-system-cmake#comment88865472_46596679
            //
            // Also, when doing so, make sure you don't include the `bin` part of the
            // path. I.e. if the path to cmake is '/X/Y/Z/bin/cmake', the use
            // cmake.dir="/X/Y/Z"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
